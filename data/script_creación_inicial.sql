use GD1C2020
go 

CREATE SCHEMA ZAFFA_FLIGHTS
GO

BEGIN TRANSACTION
CREATE TABLE ZAFFA_FLIGHTS.[butaca_tipo]
( 
	[CODIGO_BUTACA_TIPO] char(2)  NOT NULL ,
	[BUTACA_TIPO_DESCRIPCION] nvarchar(255)  NULL 
)
go

ALTER TABLE ZAFFA_FLIGHTS.[butaca_tipo]
	ADD CONSTRAINT [XPKbutaca_tipo] PRIMARY KEY  CLUSTERED ([CODIGO_BUTACA_TIPO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[sucursal]
( 
	[SUCURSAL_NUM]      INT IDENTITY(1,1)  NOT NULL ,
	[SUCURSAL_DIR]       nvarchar(255)  NULL ,
	[SUCURSAL_EMAIL]     nvarchar(255)  NULL ,
	[SUCURSAL_TELEFONO]  decimal(18, 0)  NULL 
)
go

ALTER TABLE ZAFFA_FLIGHTS.[sucursal]
	ADD CONSTRAINT [XPKsucursal] PRIMARY KEY  CLUSTERED ([SUCURSAL_NUM] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[cliente]
( 
	[CLIENTE_APELLIDO]   nvarchar(255)  NULL ,
	[CLIENTE_NOMBRE]     nvarchar(255)  NULL ,
	[CLIENTE_CODIGO]     INT IDENTITY(1,1)  NOT NULL ,
	[CLIENTE_DNI]        decimal(18, 0)  NULL ,
	[CLIENTE_FECHA_NAC]  datetime2(3)  NULL ,
	[CLIENTE_MAIL]       nvarchar(255)  NULL ,
	[CLIENTE_TELEFONO]   int  NULL 
)
go

ALTER TABLE ZAFFA_FLIGHTS.[cliente]
	ADD CONSTRAINT [XPKcliente] PRIMARY KEY  CLUSTERED ([CLIENTE_CODIGO] ASC)
go


Create TABLE ZAFFA_FLIGHTS.[ciudad]
( 
	[CODIGO_CIUDAD]      char(3)  NOT NULL ,
	[CIUDAD_DESCRIPCION] nvarchar(255)  NULL 
)
go

ALTER TABLE ZAFFA_FLIGHTS.[ciudad]
	ADD CONSTRAINT [XPKciudad] PRIMARY KEY  CLUSTERED ([CODIGO_CIUDAD] ASC)
go



CREATE TABLE ZAFFA_FLIGHTS.[tipo_habitacion]
( 
	[TIPO_HABITACION_CODIGO] decimal(18, 0)  NOT NULL ,
	[TIPO_HABITACION_DESC] nvarchar(50)  NULL 
)
go

ALTER TABLE ZAFFA_FLIGHTS.[tipo_habitacion]
	ADD CONSTRAINT [XPKtipo_habitacion] PRIMARY KEY  CLUSTERED ([TIPO_HABITACION_CODIGO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[empresa]
( 
	[EMPRESA_RAZON_SOCIAL] nvarchar(255)  NULL ,
	[EMPRESA_CODIGO]     char(3)  NOT NULL 
)
go

ALTER TABLE ZAFFA_FLIGHTS.[empresa]
	ADD CONSTRAINT [XPKempresa] PRIMARY KEY  CLUSTERED ([EMPRESA_CODIGO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[avion]
( 
	[AVION_IDENTIFICADOR] nvarchar(50)  NOT NULL ,
	[AVION_MODELO]       nvarchar(50)  NULL ,
	[EMPRESA_CODIGO]     char(3)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[empresa](EMPRESA_CODIGO)
)
go

ALTER TABLE ZAFFA_FLIGHTS.[avion]
	ADD CONSTRAINT [XPKavion] PRIMARY KEY  CLUSTERED ([AVION_IDENTIFICADOR] ASC)
go

create TABLE ZAFFA_FLIGHTS.[butaca]
( 
	[BUTACA_CODIGO]     INT IDENTITY(1,1) NOT NULL,
	[BUTACA_NUMERO]      decimal(18, 0)  NOT NULL ,
	[AVION_IDENTIFICADOR] nvarchar(50)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[avion](AVION_IDENTIFICADOR),
	[BUTACA_TIPO]        char(2)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.butaca_tipo(CODIGO_BUTACA_TIPO),
	CONSTRAINT UC_butaca UNIQUE (BUTACA_NUMERO,AVION_IDENTIFICADOR,BUTACA_TIPO) 
)
go

ALTER TABLE ZAFFA_FLIGHTS.[butaca]
	ADD CONSTRAINT [XPKbutaca] PRIMARY KEY  CLUSTERED ([BUTACA_CODIGO])
go

CREATE TABLE ZAFFA_FLIGHTS.[compra]
( 
	[COMPRA_NUMERO]      decimal(18, 0)  NOT NULL ,
	[COMPRA_FECHA]       datetime2(3)  NULL ,
	[EMPRESA_CODIGO]     char(3)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[empresa](EMPRESA_CODIGO)
)
go

ALTER TABLE ZAFFA_FLIGHTS.[compra]
	ADD CONSTRAINT [XPKcompra] PRIMARY KEY  CLUSTERED ([COMPRA_NUMERO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[hotel]
( 
	[HOTEL_CODIGO]       INT IDENTITY(1,1) NOT NULL ,
	[HOTEL_NRO_CALLE]    decimal(18, 0)  NULL ,
	[HOTEL_CANTIDAD_ESTRELLAS] decimal(18, 0)  NULL ,
	[HOTEL_CALLE]        nvarchar(50)  NULL ,
	[EMPRESA_CODIGO]     char(3)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[empresa](EMPRESA_CODIGO),
	[CODIGO_CIUDAD]      char(3)  --Debería ser FK pero está lleno de nulls porque no sabemos de donde sacar este dato
)
go


ALTER TABLE ZAFFA_FLIGHTS.[hotel]
	ADD CONSTRAINT [XPKhotel] PRIMARY KEY  CLUSTERED ([HOTEL_CODIGO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[habitaciones]
(
	[HABITACION_NUMERO]  decimal(18,0)  NOT NULL ,
	[HABITACION_PISO]    decimal(18,0)  NULL ,
	[HABITACION_FRENTE]  nvarchar(50)  NULL ,
	[HABITACION_COSTO]   decimal(18,2)  NULL ,
	[HABITACION_PRECIO]  decimal(18,2)  NULL ,
	[HOTEL_CODIGO]       INT  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[hotel](HOTEL_CODIGO),
	[TIPO_HABITACION_CODIGO] decimal(18, 0)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[tipo_habitacion](TIPO_HABITACION_CODIGO),
)
go

ALTER TABLE ZAFFA_FLIGHTS.[habitaciones]
	ADD CONSTRAINT [XPKhabitaciones] PRIMARY KEY  CLUSTERED ([HABITACION_NUMERO] ASC, [HOTEL_CODIGO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[estadia]
( 
	[ESTADIA_FECHA_INI]  datetime2(3)  NULL ,
	[ESTADIA_CANTIDAD_NOCHES] decimal(18, 0)  NULL ,
	[ESTADIA_CODIGO]     decimal(18, 0)  NOT NULL ,
	[COMPRA_NUMERO]      decimal(18, 0)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[compra](COMPRA_NUMERO) ,
	[HABITACION_NUMERO]  decimal(18, 0)  NOT NULL,
	[HOTEL_CODIGO] INT NOT NULL,
	CONSTRAINT FK_estadiaHabitacion FOREIGN KEY (HABITACION_NUMERO,HOTEL_CODIGO) REFERENCES ZAFFA_FLIGHTS.[habitaciones](HABITACION_NUMERO,HOTEL_CODIGO)
)
go

ALTER TABLE ZAFFA_FLIGHTS.[estadia]
	ADD CONSTRAINT [XPKestadia] PRIMARY KEY  CLUSTERED ([ESTADIA_CODIGO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[reserva]
( 
	[RESERVA_CODIGO]     INT IDENTITY(1,1)  NOT NULL ,
	[ESTADIA_CODIGO]     decimal(18, 0)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[estadia](ESTADIA_CODIGO)
)
go

ALTER TABLE ZAFFA_FLIGHTS.[reserva]
	ADD CONSTRAINT [XPKreserva] PRIMARY KEY  CLUSTERED ([RESERVA_CODIGO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[ruta]
( 
	[RUTA_ID] INT IDENTITY(1,1) NOT NULL ,
	[RUTA_AEREA_CODIGO]  decimal(18, 0)  NOT NULL ,
	[RUTA_AEREA_CIU_ORIG] char(3)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[ciudad](CODIGO_CIUDAD),
	[RUTA_AEREA_CIU_DEST] char(3)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[ciudad](CODIGO_CIUDAD)
)
go


ALTER TABLE ZAFFA_FLIGHTS.[ruta]
	ADD CONSTRAINT [XPKruta] PRIMARY KEY  CLUSTERED ([RUTA_ID] ASC)
go

create TABLE ZAFFA_FLIGHTS.[vuelo]
( 
	[VUELO_CODIGO]       decimal(19, 0)  NOT NULL ,
	[VUELO_FECHA_SALIDA] datetime2(3)  NULL ,
	[VUELO_FECHA_LLEGADA] datetime2(3)  NULL ,
	[AVION_IDENTIFICADOR] nvarchar(50)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[avion](AVION_IDENTIFICADOR),
	[RUTA_ID]  INT  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[ruta](RUTA_ID),
	[EMPRESA_CODIGO]     char(3)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[empresa](EMPRESA_CODIGO)
)
go

ALTER TABLE ZAFFA_FLIGHTS.[vuelo]
	ADD CONSTRAINT [XPKvuelo] PRIMARY KEY  CLUSTERED ([VUELO_CODIGO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[pasaje] 
( 
	[PASAJE_CODIGO]      decimal(18, 0)  NOT NULL ,
	[PASAJE_COSTO]       decimal(18, 2)  NULL ,
	[PASAJE_PRECIO]      decimal(18, 2)  NULL ,
	[VUELO_CODIGO]       decimal(19, 0)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[vuelo](VUELO_CODIGO),
	[COMPRA_NUMERO]      decimal(18, 0)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[compra](COMPRA_NUMERO),
	[BUTACA_CODIGO]     INT NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[butaca](BUTACA_CODIGO),
	CONSTRAINT FK_pasajeButaca FOREIGN KEY (BUTACA_CODIGO) REFERENCES ZAFFA_FLIGHTS.[butaca]([BUTACA_CODIGO])
)
go

ALTER TABLE ZAFFA_FLIGHTS.[pasaje]
	ADD CONSTRAINT [XPKpasaje] PRIMARY KEY  CLUSTERED ([PASAJE_CODIGO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[factura]
( 
	[FACTURA_NRO]        decimal(18, 0)  NOT NULL ,
	[FACTURA_FECHA]      datetime2(3)  NULL ,
	[CLIENTE_CODIGO]     int  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[cliente](CLIENTE_CODIGO),
	[PASAJE_CODIGO]      decimal(18, 0)   NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[pasaje](PASAJE_CODIGO),
	[SUCURUSAL_NUM]      INT  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[sucursal](SUCURSAL_NUM),
	[RESERVA_CODIGO]     INT   NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[reserva](RESERVA_CODIGO)
)
go

ALTER TABLE ZAFFA_FLIGHTS.[factura]
	ADD CONSTRAINT [XPKfactura] PRIMARY KEY  CLUSTERED ([FACTURA_NRO] ASC)
go

CREATE TABLE ZAFFA_FLIGHTS.[pasaje_anomalo] 
( 
	[PASAJE_CODIGO]      decimal(18, 0)  NOT NULL ,
	[PASAJE_COSTO]       decimal(18, 2)  NULL ,
	[PASAJE_PRECIO]      decimal(18, 2)  NULL ,
	[VUELO_CODIGO]       decimal(19, 0)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[vuelo](VUELO_CODIGO),
	[COMPRA_NUMERO]      decimal(18, 0)  NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[compra](COMPRA_NUMERO),
	[BUTACA_CODIGO]     INT NOT NULL FOREIGN KEY REFERENCES  ZAFFA_FLIGHTS.[butaca](BUTACA_CODIGO),
	CONSTRAINT FK_pasajeButacaAnomalo FOREIGN KEY (BUTACA_CODIGO) REFERENCES ZAFFA_FLIGHTS.[butaca]([BUTACA_CODIGO])
)
go

ALTER TABLE ZAFFA_FLIGHTS.[pasaje_anomalo]
	ADD CONSTRAINT [XPKpasaje_anomalo] PRIMARY KEY  CLUSTERED ([PASAJE_CODIGO] ASC)
go


commit

GO



CREATE FUNCTION ZAFFA_FLIGHTS.Fx_codigoEmpresa (@razonsocial nvarchar(255))
	returns char(3) 
AS BEGIN
	DECLARE @retorno char(3)
	SET @retorno = CASE
		WHEN @razonSocial = 'LATAM Airlines Group' THEN 'LAG'
		WHEN @razonSocial = 'British Airways' THEN 'BA'
		WHEN @razonSocial = 'United Airlines' THEN 'UA'
		WHEN @razonSocial = 'China Eastern Airlines' THEN 'CEA'
		WHEN @razonSocial = 'Ryanair' THEN 'RYA'
		WHEN @razonSocial = 'American Airlines' THEN 'AA'
		WHEN @razonSocial = 'Air China' THEN 'AC'
		WHEN @razonSocial = 'easyJet' THEN 'EJ'	
		WHEN @razonSocial = 'Emirates' THEN 'EM'
		WHEN @razonSocial = 'Delta Airlines' THEN 'DA'
		WHEN @razonSocial = 'Turkish Airlines' THEN 'TA'
		WHEN @razonSocial = 'All Nippon Airways' THEN 'ANA'	
		WHEN @razonSocial = 'China Southern Airlines' THEN 'CSA'
		WHEN @razonsocial = 'AccorHotels' THEN 'AH'
		WHEN @razonSocial = 'Choice Hotels International Inc' THEN 'CHI'
		WHEN @razonsocial = 'Shanghai Jin Jiang International Hotel Group Inc.' THEN 'SJJ'
		WHEN @razonsocial = 'Best Western Hotels & Resorts' THEN 'BWH'
		WHEN @razonsocial = 'Marriott International' THEN 'MI'
		WHEN @razonsocial = 'Hilton' THEN 'HIL'
		WHEN @razonsocial = 'BTG Hommeins Hotels Group' THEN 'BTG'
		WHEN @razonsocial = 'China Lodging Group' THEN 'CLG'
		WHEN @razonsocial = 'Wyndham Hotels Group' THEN 'WHG'
	END
	RETURN @retorno
END
GO

CREATE FUNCTION ZAFFA_FLIGHTS.Fx_codigoButaca (@butaca_tipo nvarchar(255))
	returns char(2)
AS BEGIN
	DECLARE @retorno char(2)
	SET @retorno= CASE 
		WHEN @butaca_tipo = 'Ejecutiva' THEN 'EJ'
		WHEN @butaca_tipo = 'Cabina Principal' THEN 'CP'
		WHEN @butaca_tipo = 'Economica Premium' THEN 'EP'
		WHEN @butaca_tipo = 'Economica Basica' THEN 'EB'
	END
	RETURN @retorno
END
GO

CREATE FUNCTION ZAFFA_FLIGHTS.Fx_codigoCiudad (@ciudad nvarchar(255))
RETURNS char(3) AS 
BEGIN
	DECLARE @retorno char(3)

	SET @retorno= CASE 
		WHEN @ciudad = 'ABUYA' THEN 'ABU'
		WHEN @ciudad = 'ACCRA' THEN 'ACC'
		WHEN @ciudad = 'ADÍS ABEBA' THEN 'ADI'
		WHEN @ciudad = 'ANTANANARIVO' THEN 'ANT'
		WHEN @ciudad = 'ARGEL' THEN 'ARG'
		WHEN @ciudad = 'ASMARA' THEN 'ASM'
		WHEN @ciudad = 'BAMAKO' THEN 'BAM'
		WHEN @ciudad = 'BANGUI' THEN 'BAN'
		WHEN @ciudad = 'BANJUL' THEN 'BAJ'
		WHEN @ciudad = 'BISSAU' THEN 'BIS'
		WHEN @ciudad = 'BRAZZAVILLE' THEN 'BRA'
		WHEN @ciudad = 'BUYUMBURA' THEN 'BUY'
		WHEN @ciudad = 'CONAKRY' THEN 'CON'
		WHEN @ciudad = 'EL AAIÚN' THEN 'ELA'
		WHEN @ciudad = 'EL CAIRO' THEN 'ELC'
		WHEN @ciudad = 'GABERONES' THEN 'GAB'
		WHEN @ciudad = 'KINSHASA' THEN 'KIN'
		WHEN @ciudad = 'LIBREVILLE' THEN 'LIB'
		WHEN @ciudad = 'LILONGÜE' THEN 'LIL'
		WHEN @ciudad = 'LUANDA' THEN 'LUA'
		WHEN @ciudad = 'MALABO' THEN 'MAL'
		WHEN @ciudad = 'MAPUTO' THEN 'MAP'
		WHEN @ciudad = 'MASERU' THEN 'MAS'
		WHEN @ciudad = 'MONROVIA' THEN 'MON'
		WHEN @ciudad = 'MORONI' THEN 'MOR'
		WHEN @ciudad = 'NAIROBI' THEN 'NAI'
		WHEN @ciudad = 'NIAMEY' THEN 'NIA'
		WHEN @ciudad = 'NUAKCHOT' THEN 'NUA'
		WHEN @ciudad = 'PORT LOUIS' THEN 'POL'
		WHEN @ciudad = 'PORTO-NOVO' THEN 'PON'
		WHEN @ciudad = 'PRAIA' THEN 'PRA'
		WHEN @ciudad = 'RABAT' THEN 'RAB'
		WHEN @ciudad = 'TRÍPOLI' THEN 'TRI'
		WHEN @ciudad = 'UAGADUGÚ' THEN 'UAG'
		WHEN @ciudad = 'WINDHOEK' THEN 'WIN'
		WHEN @ciudad = 'YAMENA' THEN 'YAE'
		WHEN @ciudad = 'YAMUSUKRO, ABIYÁN' THEN 'YAA'
		WHEN @ciudad = 'YAUNDÉ' THEN 'YAU'
	END

	RETURN @retorno
END
GO

CREATE PROCEDURE ZAFFA_FLIGHTS.ejecutar_migracion
AS BEGIN 

	-- CREACION TABLAS TEMPORALES

	SELECT DISTINCT COMPRA_NUMERO, COMPRA_FECHA, EMPRESA_RAZON_SOCIAL, ESTADIA_FECHA_INI, ESTADIA_CANTIDAD_NOCHES, ESTADIA_CODIGO, HOTEL_CALLE, HOTEL_NRO_CALLE, HOTEL_CANTIDAD_ESTRELLAS, 
		HABITACION_NUMERO, HABITACION_PISO, HABITACION_FRENTE, HABITACION_COSTO, HABITACION_PRECIO, TIPO_HABITACION_CODIGO, TIPO_HABITACION_DESC
	INTO #ESTADIAS_EN_GENERAL
	FROM gd_esquema.Maestra
	WHERE ESTADIA_CODIGO IS NOT NULL
		
	SELECT DISTINCT COMPRA_NUMERO, COMPRA_FECHA, EMPRESA_RAZON_SOCIAL, VUELO_CODIGO, VUELO_FECHA_SALUDA, VUELO_FECHA_LLEGADA, 
		RUTA_AEREA_CIU_DEST, RUTA_AEREA_CIU_ORIG, RUTA_AEREA_CODIGO, BUTACA_NUMERO, BUTACA_TIPO,AVION_IDENTIFICADOR, 
		AVION_MODELO,PASAJE_CODIGO, PASAJE_COSTO,PASAJE_PRECIO, PASAJE_FECHA_COMPRA
	INTO #VUELOS_EN_GENERAL
	FROM gd_esquema.Maestra
	WHERE AVION_MODELO IS NOT NULL


	SELECT DISTINCT FACTURA_FECHA, FACTURA_NRO,CLIENTE_APELLIDO,CLIENTE_NOMBRE,
		CLIENTE_DNI,CLIENTE_FECHA_NAC,CLIENTE_MAIL,CLIENTE_TELEFONO,SUCURSAL_DIR,SUCURSAL_MAIL,SUCURSAL_TELEFONO
	INTO #SOBRE_CLIENTES
	FROM gd_esquema.Maestra
	WHERE CLIENTE_NOMBRE IS NOT NULL

	-- INSERTS EN TABLAS

	INSERT INTO ZAFFA_FLIGHTS.empresa (EMPRESA_RAZON_SOCIAL, EMPRESA_CODIGO)
		SELECT DISTINCT EMPRESA_RAZON_SOCIAL, ZAFFA_FLIGHTS.fx_codigoEmpresa(EMPRESA_RAZON_SOCIAL) 
		FROM #VUELOS_EN_GENERAL
		UNION
		SELECT DISTINCT EMPRESA_RAZON_SOCIAL,ZAFFA_FLIGHTS.fx_codigoEmpresa(EMPRESA_RAZON_SOCIAL) 
		FROM #ESTADIAS_EN_GENERAL

	INSERT INTO ZAFFA_FLIGHTS.compra ( COMPRA_NUMERO, EMPRESA_CODIGO, COMPRA_FECHA )
		SELECT DISTINCT COMPRA_NUMERO ,ZAFFA_FLIGHTS.fx_codigoEmpresa(EMPRESA_RAZON_SOCIAL), COMPRA_FECHA
		FROM  #VUELOS_EN_GENERAL
		UNION
		SELECT DISTINCT COMPRA_NUMERO ,ZAFFA_FLIGHTS.fx_codigoEmpresa(EMPRESA_RAZON_SOCIAL), COMPRA_FECHA
		FROM #ESTADIAS_EN_GENERAL

	INSERT INTO ZAFFA_FLIGHTS.avion (AVION_IDENTIFICADOR, AVION_MODELO, EMPRESA_CODIGO) 
		SELECT DISTINCT AVION_IDENTIFICADOR, AVION_MODELO, ZAFFA_FLIGHTS.fx_codigoEmpresa(EMPRESA_RAZON_SOCIAL) EMPRESA_CODIGO
		FROM #VUELOS_EN_GENERAL


	INSERT INTO ZAFFA_FLIGHTS.ciudad (CODIGO_CIUDAD,CIUDAD_DESCRIPCION) --hacer
		SELECT DISTINCT ZAFFA_FLIGHTS.Fx_codigoCiudad(RUTA_AEREA_CIU_ORIG), RUTA_AEREA_CIU_ORIG
		FROM #VUELOS_EN_GENERAL


	INSERT INTO ZAFFA_FLIGHTS.ruta (RUTA_AEREA_CODIGO, RUTA_AEREA_CIU_ORIG, RUTA_AEREA_CIU_DEST) 
		SELECT DISTINCT RUTA_AEREA_CODIGO, ZAFFA_FLIGHTS.Fx_codigoCiudad(RUTA_AEREA_CIU_ORIG), ZAFFA_FLIGHTS.Fx_codigoCiudad(RUTA_AEREA_CIU_DEST) 
		FROM #VUELOS_EN_GENERAL
	

	INSERT INTO ZAFFA_FLIGHTS.vuelo (VUELO_CODIGO,EMPRESA_CODIGO,AVION_IDENTIFICADOR,RUTA_ID,VUELO_FECHA_SALIDA,VUELO_FECHA_LLEGADA)
		SELECT DISTINCT VUELO_CODIGO,ZAFFA_FLIGHTS.fx_codigoEmpresa(EMPRESA_RAZON_SOCIAL),AVION_IDENTIFICADOR,r.RUTA_ID,VUELO_FECHA_SALUDA,VUELO_FECHA_LLEGADA
		FROM #VUELOS_EN_GENERAL vg INNER JOIN ZAFFA_FLIGHTS.ruta r ON (vg.RUTA_AEREA_CODIGO = r.RUTA_AEREA_CODIGO AND ZAFFA_FLIGHTS.Fx_codigoCiudad(vg.RUTA_AEREA_CIU_ORIG) = r.RUTA_AEREA_CIU_ORIG)


	INSERT INTO ZAFFA_FLIGHTS.butaca_tipo (CODIGO_BUTACA_TIPO,BUTACA_TIPO_DESCRIPCION)
		SELECT DISTINCT ZAFFA_FLIGHTS.Fx_codigoButaca(BUTACA_TIPO), BUTACA_TIPO
		FROM #VUELOS_EN_GENERAL


	INSERT INTO ZAFFA_FLIGHTS.butaca (BUTACA_NUMERO,AVION_IDENTIFICADOR,BUTACA_TIPO)
		SELECT DISTINCT BUTACA_NUMERO,AVION_IDENTIFICADOR, ZAFFA_FLIGHTS.Fx_codigoButaca(BUTACA_TIPO)
		FROM #VUELOS_EN_GENERAL
		WHERE BUTACA_TIPO IS NOT NULL


	INSERT INTO ZAFFA_FLIGHTS.pasaje (PASAJE_CODIGO,PASAJE_COSTO,BUTACA_CODIGO,PASAJE_PRECIO,VUELO_CODIGO,COMPRA_NUMERO) 
		SELECT DISTINCT PASAJE_CODIGO, PASAJE_COSTO, BUTACA_CODIGO, PASAJE_PRECIO, VUELO_CODIGO, COMPRA_NUMERO
		FROM #VUELOS_EN_GENERAL vg INNER JOIN ZAFFA_FLIGHTS.butaca b ON vg.AVION_IDENTIFICADOR = b.AVION_IDENTIFICADOR 
																			AND vg.BUTACA_NUMERO = b.BUTACA_NUMERO
																			AND ZAFFA_FLIGHTS.Fx_codigoButaca(vg.BUTACA_TIPO) = b.BUTACA_TIPO
	
	
	------------------------------------------------------------------Estadias
	
	INSERT INTO ZAFFA_FLIGHTS.hotel (HOTEL_CALLE,HOTEL_NRO_CALLE,CODIGO_CIUDAD,EMPRESA_CODIGO,HOTEL_CANTIDAD_ESTRELLAS)
		SELECT DISTINCT HOTEL_CALLE,HOTEL_NRO_CALLE,NULL,ZAFFA_FLIGHTS.fx_codigoEmpresa(EMPRESA_RAZON_SOCIAL) ,HOTEL_CANTIDAD_ESTRELLAS
		FROM #ESTADIAS_EN_GENERAL


	INSERT INTO ZAFFA_FLIGHTS.tipo_habitacion (TIPO_HABITACION_CODIGO,TIPO_HABITACION_DESC)
		SELECT DISTINCT TIPO_HABITACION_CODIGO,TIPO_HABITACION_DESC
		FROM #ESTADIAS_EN_GENERAL


	INSERT INTO ZAFFA_FLIGHTS.habitaciones (HABITACION_NUMERO,HOTEL_CODIGO,HABITACION_FRENTE,TIPO_HABITACION_CODIGO,HABITACION_PISO,HABITACION_COSTO,HABITACION_PRECIO)
		SELECT DISTINCT e.HABITACION_NUMERO,HOTEL_CODIGO,e.HABITACION_FRENTE,e.TIPO_HABITACION_CODIGO,e.HABITACION_PISO,e.HABITACION_COSTO,e.HABITACION_PRECIO
		FROM #ESTADIAS_EN_GENERAL e INNER JOIN ZAFFA_FLIGHTS.hotel h ON (e.HOTEL_CALLE=h.HOTEL_CALLE AND e.HOTEL_NRO_CALLE = h.HOTEL_NRO_CALLE)


	INSERT INTO ZAFFA_FLIGHTS.estadia (ESTADIA_CODIGO, HABITACION_NUMERO, ESTADIA_FECHA_INI, ESTADIA_CANTIDAD_NOCHES, COMPRA_NUMERO, HOTEL_CODIGO)
		SELECT DISTINCT ESTADIA_CODIGO,HABITACION_NUMERO,ESTADIA_FECHA_INI,ESTADIA_CANTIDAD_NOCHES,COMPRA_NUMERO, h.HOTEL_CODIGO
		FROM #ESTADIAS_EN_GENERAL eg INNER JOIN ZAFFA_FLIGHTS.hotel h ON eg.HOTEL_CALLE = h.HOTEL_CALLE 
																			AND eg.HOTEL_NRO_CALLE = h.HOTEL_NRO_CALLE


	INSERT INTO ZAFFA_FLIGHTS.reserva (ESTADIA_CODIGO)
		SELECT DISTINCT ESTADIA_CODIGO 
		FROM gd_esquema.Maestra 
		WHERE FACTURA_NRO IS NOT NULL AND ESTADIA_CODIGO IS NOT NULL
	

	INSERT INTO ZAFFA_FLIGHTS.cliente (CLIENTE_APELLIDO,CLIENTE_NOMBRE,CLIENTE_DNI,CLIENTE_FECHA_NAC,CLIENTE_MAIL,CLIENTE_TELEFONO)
		SELECT DISTINCT CLIENTE_APELLIDO,CLIENTE_NOMBRE,CLIENTE_DNI,CLIENTE_FECHA_NAC,CLIENTE_MAIL,CLIENTE_TELEFONO
		FROM #SOBRE_CLIENTES


	INSERT INTO ZAFFA_FLIGHTS.sucursal (SUCURSAL_DIR,SUCURSAL_EMAIL,SUCURSAL_TELEFONO)
		SELECT DISTINCT SUCURSAL_DIR,SUCURSAL_MAIL,SUCURSAL_TELEFONO
		FROM #SOBRE_CLIENTES
	
	create index reserva
	on ZAFFA_FLIGHTS.reserva (ESTADIA_CODIGO)


	INSERT INTO ZAFFA_FLIGHTS.factura (FACTURA_NRO, CLIENTE_CODIGO, RESERVA_CODIGO, SUCURUSAL_NUM, FACTURA_FECHA, PASAJE_CODIGO)
		SELECT DISTINCT m.FACTURA_NRO,c.CLIENTE_CODIGO, r.RESERVA_CODIGO, s.SUCURSAL_NUM, m.FACTURA_FECHA, m.PASAJE_CODIGO
		FROM gd_esquema.Maestra m INNER JOIN ZAFFA_FLIGHTS.cliente c ON (m.CLIENTE_DNI = c.CLIENTE_DNI AND M.CLIENTE_TELEFONO = c.CLIENTE_TELEFONO)
								  INNER JOIN ZAFFA_FLIGHTS.sucursal s ON (m.SUCURSAL_MAIL = s.SUCURSAL_EMAIL)
								  LEFT JOIN ZAFFA_FLIGHTS.reserva r ON (m.ESTADIA_CODIGO = r.ESTADIA_CODIGO)
		WHERE m.FACTURA_NRO IS NOT NULL 

		
END
GO

create procedure ZAFFA_FLIGHTS.anomaliaPasaje
as begin 

	insert into ZAFFA_FLIGHTS.pasaje_anomalo (PASAJE_CODIGO, PASAJE_COSTO, PASAJE_PRECIO, VUELO_CODIGO, COMPRA_NUMERO, BUTACA_CODIGO)
	select p.PASAJE_CODIGO, p.PASAJE_COSTO, p.PASAJE_PRECIO, p.VUELO_CODIGO, p.COMPRA_NUMERO, p.BUTACA_CODIGO
	FROM ZAFFA_FLIGHTS.pasaje p
	where (p.VUELO_CODIGO in (select VUELO_CODIGO  
								from ZAFFA_FLIGHTS.pasaje
								GROUP BY VUELO_CODIGO, BUTACA_CODIGO
								having count(PASAJE_CODIGO) >1) )
								and
		    (p.BUTACA_CODIGO in (select BUTACA_CODIGO  
								from ZAFFA_FLIGHTS.pasaje
								GROUP BY VUELO_CODIGO, BUTACA_CODIGO
								having count(PASAJE_CODIGO) >1) )

end
go
execute ZAFFA_FLIGHTS.ejecutar_migracion
go
execute ZAFFA_FLIGHTS.anomaliaPasaje